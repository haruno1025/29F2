-- test
class 人

instance variables
protected iマイナンバー : token;
protected i年齢 : nat;
protected iＩＣカード乗車券 : ＩＣカード乗車券;

operations
public 人 : token * nat ==> 人
人(aマイナンバー, a年齢) ==
  atomic(
    iマイナンバー := aマイナンバー;
    i年齢 := a年齢;
  );

public ICカードを購入する : 駅 ==> ()
ICカードを購入する(a駅) ==
  atomic(
    iＩＣカード乗車券 := a駅.ＩＣカード乗車券を販売する(i年齢);
  )
pre
	i年齢 >= 6;

public 駅へ入場する : 駅 ==> ()
駅へ入場する(a駅) == a駅.get改札機等().入場する(iＩＣカード乗車券);

public 駅から退場する : 駅 ==> ()
駅から退場する(a駅) == a駅.get改札機等().退場する(iＩＣカード乗車券);

public チャージする : 駅 * nat1 ==> ()
チャージする(a駅, a入金額) == (
  iＩＣカード乗車券 := a駅.iＩＣカード乗車券を処理する機器.ＩＣカードにチャージする(iＩＣカード乗車券, a入金額);
);

public pure getマイナンバー : () ==> token
getマイナンバー() == return iマイナンバー;

public pure getＩＣカード乗車券 : () ==> ＩＣカード乗車券
getＩＣカード乗車券() == return iＩＣカード乗車券;

end 人