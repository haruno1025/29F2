------------------------------------------------------------------------
--  第６条　この規則における普通旅客運賃は、前条第１項の定めにより乗車した場合適用する運賃
--        をいう。
------------------------------------------------------------------------

class 第１編第６条（運賃）

types
--	public 年齢別旅客区分型 = <大人>　| <小人> | <幼児> | <乳児>;
	
values
public 当社線 : set of token = {};
-- TODO Define values here

instance variables
--	public i年齢別旅客区分 : 年齢別旅客区分型;
--	public i乗車駅 : set of メトロ;
--	public i降車駅 : set of メトロ;
--	public i相互発着 : set of メトロ

-- (溜池山王, 虎ノ門) |-> 100(円)
protected 相互発着写像　: map (token * token ) to nat1 := {|->};

-- 駅の組からの距離 想定:(溜池山王, 虎ノ門) |-> 0.8(km)
protected キロ程写像　: map (token * token ) to set of real := {|->};

-- 駅の組からの運賃 想定:(溜池山王, 虎ノ門) |-> 100(円)
protected 普通旅客運賃写像　: map (token * token ) to nat1 := {|->};
/*
inv
  dom(普通旅客運賃写像).(a,b)  in set 当社線
;
*/

-- 第２項と第４項はともに大人運賃の金額を言っていると読み取れるが、
-- 双方が一致しているかは今のところ読み取れない

operations


	/*********************************************************************
	 * Operation名　：　get運賃
	 *　概要　　　　　　  ：　乗車駅（a乗車駅）と降車駅（b降車駅）を引数として運賃を返却する 
	 *
	 *　[規定]
	 * ２　前項に定める普通旅客運賃のうち、大人片道普通旅客運賃は、旅客の乗車する発着区間
	 *　　　キロ程により、次によって区分した１円単位運賃とする。
	 *　　　　　１区 　１キロメートルから　　６キロメートルまで　　１６５円
	 *　　　　　２区 　７キロメートルから　１１キロメートルまで　　１９５円
	 *　　　　　３区 １２キロメートルから　１９キロメートルまで　　２３７円
	 *　　　　　４区 ２０キロメートルから　２７キロメートルまで　　２７８円
	 *　　　　　５区 ２８キロメートルから　４０キロメートルまで　　３０８円
	 *　　　ただし、綾瀬・北千住間相互発着となる場合及び目黒・白金高輪間相互発着となる場合は、
	 *　　　キロ程によらず、次の各号のとおりとする。
	 *　　　　　（１）綾瀬・北千住間相互発着となる場合　　　１３３円
	 *　　　　　（２）目黒・白金高輪間相互発着となる場合　  １６５円
	**********************************************************************/
  -- 6条2項
	public pure get運賃 : token * token ==> nat1
	get運賃(a乗車駅, a降車駅) == 
	 (dcl l最短キロ程 : real, lキロ程 : nat, l大人片道普通旅客運賃 : nat1;
		-- 相互発着の場合
	 	if mk_(a乗車駅, a降車駅) in set dom(相互発着写像) then
	    l大人片道普通旅客運賃 := 相互発着写像(mk_(a乗車駅, a降車駅))
		-- 相互発着ではない場合
	  else
　　　　　-- todo 6条3項
		  l最短キロ程 := 最短のキロ程を取得する(キロ程写像(mk_(a乗車駅, a降車駅)));
		  lキロ程 := 端数を１キロ単位に切り上げ(l最短キロ程);
　　　　　-- 6条2項
		  if 1 <= lキロ程 and lキロ程 <= 6 then
		    l大人片道普通旅客運賃 := 165
		  else if 7 <= lキロ程 and lキロ程 <= 11 then
		    l大人片道普通旅客運賃 := 195
		  else if 12 <= lキロ程 and lキロ程 <= 19 then
		    l大人片道普通旅客運賃 := 237
		  else if 20 <= lキロ程 and lキロ程 <= 27 then
		    l大人片道普通旅客運賃 := 278
		  else if 28 <= lキロ程 and lキロ程 <= 40 then
		    l大人片道普通旅客運賃 := 308;

    return l大人片道普通旅客運賃; 		  
	)
	pre
		a乗車駅 <> a降車駅 and
		(	mk_(a乗車駅, a降車駅) in set dom(キロ程写像) or
			mk_(a乗車駅, a降車駅) in set dom(相互発着写像))
;

  -- 6条4項
	public pure get運賃6条4項 : token * token ==> nat1
	get運賃6条4項(a乗車駅, a降車駅) == return 普通旅客運賃写像(mk_(a乗車駅, a降車駅))
	pre
		a乗車駅 <> a降車駅 and
		(	mk_(a乗車駅, a降車駅) in set dom(普通旅客運賃写像));

  -- 第６条の２ 小児片道普通旅客運賃
	public pure get運賃６条の２ : nat1 ==> nat1
	get運賃６条の２(a大人片道普通旅客運賃) == return floor(a大人片道普通旅客運賃　/ 2) 
	pre
		a大人片道普通旅客運賃 in set rng(普通旅客運賃写像);

--第６条の３ この規則に規定する運賃については、消費税法（昭和６３年法律第１０８号）の定めによる消費税相当額
--及び地方税法（昭和２５年法律第２２６号）の定めによる地方消費税相当額を含んだ額とする。

--第７条 記名ＩＣカードに係る個人情報の取扱いは、ＩＣカード発行事業者の定めるところによる。

--第８条 旅客は、この規則及びこれに基づいて定められた規定を承認し、かつ、これに同意したものとする。

--第９条 当社におけるＩＣカード乗車券の取扱区間は、営業規程第３条第１号に規定する当社線とする。


functions
public static 最短のキロ程を取得する : set of real -> real
最短のキロ程を取得する(aキロ程) == (
	let w最短のキロ程 in set aキロ程 be st forall wキロ程 in set aキロ程 & w最短のキロ程 <= wキロ程
	in w最短のキロ程
)
pre 
	aキロ程 <> {}
post
	forall wキロ程　in set aキロ程 & RESULT <= wキロ程
;

public 端数を１キロ単位に切り上げ : real +> nat
端数を１キロ単位に切り上げ(aキロ程) ==
  if floor aキロ程 = aキロ程 then
    floor aキロ程
  else
    floor (aキロ程 + 1)
pre
  aキロ程 >= 0
;

--    
traces
-- TODO Define Combinatorial Test Traces here

end 第１編第６条（運賃）